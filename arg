#!/usr/bin/perl -w
#
# arg - print specified arguments (columns) from each line of input
#

use strict;

sub usage () {

(my $my_name = $0) =~ s:^.*/::g;
die <<EOT;
Usage: $my_name [-b] LIST_OF_COLUMNS
  1 3      - 1st & 3rd
  0        - Print all arguments in the line (= 1--1)
  -0       - Print arguments in the line reversed (= -1-1)
  1 0      - Print 1st argument followed by all the arguments (incl 1st again)

forward ranges:
  1-3      - 1st to 3rd
  1--3     - 1st to 3rd last
  -3--1    - 3rd last to last
backward ranges:
  3-1      - 3rd to 1st
  -3-1     - 3rd last to 1st
  -1--3    - Last to 3rd last
  -1-3     - Last to 3rd

  -b       - Include blank lines
  -c       - Output in CSV format instead of space seperated
  -fSEP    - Input seperator
  =STRING= - Literal string "STRING"
  NF|c|-#  - Number of fields in line
  -t       - Trim leading spaces on line
  -u       - This usage message
  -h       - Help (this usage message)

EOT

}

my @args;
my %opts = (
   b => 0,
   e => 0,
   s => ' ',
);
my $sep = '\s+';

for my $argv ( @ARGV ) {

    my @chunks = split(/\s+/, $argv);

    for my $chunk ( @chunks ) {

        if ( $chunk eq '0' ) {

            push @args, 'LINE';

        }
        elsif ( $chunk eq '-0' ) {

            push @args, 'REV';

        }
        elsif ( $chunk =~ /^-?\d+$/ ) {

            $chunk-- if $chunk > -1;
            push @args, $chunk;
            next;

        }
        elsif ( $chunk =~ /^(\d+)-(\d+)$/ ) {

            my ($from, $to) = ($1, $2);

            $from--;
            $to--;

            if ( $from < $to ) {

                push @args, ($from .. $to);

            }
            else {

                push @args, reverse ( $to .. $from );

            }

        }
        elsif ( $chunk =~ /^(-?\d+)-(-?\d+)$/ ) {

            $opts{e} = 1;
            push @args, "E$chunk";
            
        }
        elsif ( $chunk eq 'NF' || $chunk eq 'c' || $chunk eq '-#' ) {

            push @args, 'NF';

        }
        elsif ( $chunk eq '-b' ) {

            $opts{b} = 1;

        }
        elsif ( $chunk eq '-c' ) {

            #
            # Set seperator to CSV
            #
            $opts{s} = ',';

        }
        elsif ( $chunk eq '-t' ) {

            $opts{t} = 1;

        }
        elsif ( $chunk eq '-u' || $chunk eq '-h' ) {

            usage;

        }
        elsif ( $chunk =~ /^=.*=$/ ) {

            #
            # Literal string
            #
            push @args, $chunk;

        }
        elsif ( $chunk =~ /^-f(.*)$/ ) {

            $sep = $1;

        }
        else {

           die "$0: Arg '$chunk' isn't an integer.\n";

        }

    }

}

while ( <STDIN> ) {

    chomp;

    if ( $opts{t} ) {
        s/^\s+//g;
    }

    my @line = split(/$sep/, $_);
    my @out;

    for my $arg (@args) {

        if ( $arg =~ /^=(.*)=$/ ) {

            #
            # Literal string
            #
            push @out, $1;

        }
        elsif ( $arg eq 'NF' ) {

            push @out, $#line + 1;

        }
        elsif ( $arg eq 'REV' ) {

            push @out, reverse @line;

        }
        elsif ( $arg eq 'LINE' ) {

            push @out, @line;

        }
        elsif ( $opts{e} ) {

            #
            # Handle ranges with negatives
            #
            if ( $arg =~ /^E(-?\d+)-(-?\d+)$/ ) {

                my @range;

                my ($from, $to) = ($1, $2);

                if ( $from =~ /^-/ ) {
                    $from = $#line + $from +1;
                }
                else {
                    $from--;
                }

                if ( $to =~ /^-/ ) {
                    $to = $#line + $to +1;
                }
                else {
                    $to--;
                }

                if ( $from < $to ) {

                    push @range, ($from .. $to);

                }
                else {

                    push @range, reverse ( $to .. $from );

                }

                for my $num (@range) {

                    push @out, $line[$num] if $line[$num];

                }

            }
            else {

                push @out, $line[$arg] if $line[$arg];

            }

        }
        else {

            push @out, $line[$arg] if $line[$arg];

        }

    }

    #
    # Don't print blank lines unless -b is specified
    #
    print join ($opts{s}, @out) , $/ if $opts{b} || @out;

}

