#!/usr/bin/perl
#
# only-in
# find lines which are in the first file, but not in the second.
#
use warnings;
use strict;
use Getopt::Long;

my %opts;
GetOptions(
    'input=s'   => \$opts{input},
    'exclude=s' => \$opts{exclude},
    'usage'     => \$opts{help},
    'help'      => \$opts{help},
);

my ($arg_1, $arg_2) = (shift, shift);
if ( ! $opts{input} ) {
    $opts{input} = $arg_1 if $arg_1;
}
if ( ! $opts{exclude} ) {
    $opts{exclude} = $arg_2 if $arg_2;
}

die "Usage: $0 [-input] INPUT [-exclude] EXCLUDE\n"
    if ! $opts{input} || ! $opts{exclude};

my %exclude;

open (my $exclude_fh, $opts{exclude} ) ||
    die "Can't read exclude file '$opts{exclude}': $!\n";
while (defined(my $exclude = <$exclude_fh>)) {
    $exclude{$exclude} = 1;
}
close $exclude_fh;

open (my $input_fh, $opts{input}) ||
    die "Can't read input file '$opts{input}': $!\n";
while (defined(my $input = <$input_fh>)) {
    print $input if ! $exclude{$input};
}
close $input_fh;

