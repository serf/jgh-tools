#!/usr/bin/perl
#
# John Harrison 2014-03-24 For Dave
#
use warnings;
use strict;
use Getopt::Std;

my %opts;
getopts('w:d:h:m:s:f:p:', \%opts);

my $age = 0;

$age += $opts{w} * 604800 if $opts{w};
$age += $opts{d} * 86400 if $opts{d};
$age += $opts{h} * 3600 if $opts{h};
$age += $opts{m} * 60 if $opts{m};
$age += $opts{s} if $opts{s};

die <<EOE if ! $age;
Usage: $0 TIME
Where time can be any (or all of):
  -w WEEKS
  -d DAYS
  -h HOURS
  -m MINUTES
  -s SECONDS

  -f FILENAME_MATCH (Uses Perl regular expression syntax.)
  -p PATH (defaults to ".")

(e.g. 'newer -d 4 -h 6 -m 23' will list files newer than 4 days, 6 hours & 23 minutes.)
EOE

my $time = time;
#
#
#
my $path = '.';
$path = $opts{p} if $opts{p};

my %files;
opendir(my $dir_fh, $path) || die "Can't opendir '$path': $!\n";
FILE: for my $file ( grep { /^./ && -f "$path/$_" } readdir($dir_fh) ) {
    if ( $opts{f} ) {
        next FILE if $file !~ /$opts{f}/;
    }
    my ($dev,$ino,$mode,$nlink,$uid,$gid,$rdev,$size,
        $atime,$mtime,$ctime,$blksize,$blocks) = stat("$path/$file");
    if ( $mtime > $time - $age ) {
        $files{$file} = $mtime;
    }
}
closedir $dir_fh;

for my $file (sort { $files{$a} <=> $files{$b} } keys %files) {
    print "$file\n";
}

